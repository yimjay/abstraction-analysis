include ExpStmt.incl
include Loops.incl
include omp.incl
include analysis.pi

<*
   POET : Parameterized Optimizations for Empirical Tuning
   Copyright (c)  2008,  Qing Yi.  All rights reserved.

Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:

 1. Redistributions of source code must retain the above copyright notice,
    this list of conditions and the following disclaimer.
 2. Redistributions in binary form must reproduce the above copyright notice,
    this list of conditions and the following disclaimer in the documentation
    and/or other materials provided with the distribution.
 3. Neither the name of UTSA nor the names of its contributors may be used to
    endorse or promote products derived from this software without specific
    prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED.
IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT,
INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY
OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE
OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISEDOF THE POSSIBILITY OF SUCH DAMAGE.
*>

<*****************************************************
 syntax descriptions for the Fortran language
******************************************************>

<define KEYWORDS ("SUBROUTINE" "CHARACTER" "continue" "CONTINUE" "return" "RETURN" "while" "WHILE" "end" "END" "enddo" "ENDDO" "endif" "ENDIF" "do" "DO" "if" "IF" "else" "ELSE" "dimension" "DIMENSION" "implicit" "IMPLICIT" "double" "DOUBLE" "precision" "PRECISION" "call" "CALL")/>

<define DECLARATION CODE.SubroutineDecl />

<define FSTMT CODE.Label|CODE.NestDO|CODE.NestIf|CODE.CallStmt|CODE.Return|CODE.ParamDeclStmt|CODE.ImplicitDeclStmt|CODE.WriteStmt|CODE.ExpStmt|CODE.VarDeclStmt|";" />

<code Stmt parse=FSTMT />

<code D0FLOAT pars=(val:FLOAT,pre:"D0"|"d0"|"")
        rebuild=((pre=="")?val:D0FLOAT#(val,pre))> @val pre@ </code>

<code F95_Skip_Linebreak parse="&" rebuild=""/> 
<code F95Comment pars=(content:(~"\n")...) rebuild="" >
!@content@
</code>

<define TOKEN ((":"":") ("=""=") ("=" ">") ("." "true" ".") ("." "TRUE" ".") ("." "false" ".") ("." "FALSE" ".") ("*" "*") ("." "not" ".") ("/" "=") ("." "NOT" ".") ("." "and" ".") ("." "AND" ".") ("." "or" ".") ("." "OR" ".") ("." "eq" ".") ("." "EQ" ".") ("." "ne" ".") ("." "NE" ".") ("." "gt" ".") ("." "GT" ".") ("." "lt" ".") ("." "LT" ".") ("." "le" ".") ("." "LE" ".") ("." "ge" ".") ("." "GE" ".") CODE.EndIf CODE.EndDo CODE.F95_Skip_Linebreak CODE.DOUBLE CODE.Char CODE.F95Comment)/>

<code EndIf parse=("end" " " "if")|("END" " " "IF")|"endif"  rebuild="endif"/> 
<code EndDo parse=("end" " " "do")|("END" " " "DO")  rebuild="enddo" />

<define EXP_BASE INT|D0FLOAT|CODE.VarRef|CODE.Char|".true."|".false."|".TRUE."|".FALSE."|CODE.DOUBLE|("(" EXP ")") />
<define EXP_BOP (("=" "=>") "%" (".and." ".AND." ".or." ".OR.") (".eq." "==" ".ne." ".gt." ".GT." ".lt." ".LT" ".le." ".ge." "<" ">" "/=" ) ("+" "-") ("*" "/") ("**") ) />
<define EXP_UOP (".not." ".NOT." "+" "-") />
<define PARSE_CALL XFORM.BuildCall/>

<code VarRef pars=(name : ID, dim : CODE.ArrayDim|"") rebuild=(XFORM.BuildCall(name,dim))> 
@name dim@ </code>

<code Dimension pars=(dims : "*"|LIST(EXP|":",",")) > (@dims@) </code>

<xform F_buildBop pars=(op, lhs, rhs)>
switch (op) {
case "=" : Assign#(lhs, rhs)
default: Bop#(op,lhs,rhs)
}
</xform>
<define BUILD_BOP F_buildBop/>

<xform BuildCall pars=(name,args) tab=GLOBAL.SymTable>
if (args == "") return name;
switch (t=get_type(name)) 
{
  case "": ArrayAccess#(name,args)
  case TAG#(_,CODE.ArrayType)|CODE.ArrayType: ArrayAccess#(name,args)
  case TAG#("FUN",_): FunctionCall#(name,args)
  default : insert_typeInfo(TAG#("FUN",t),name); FunctionCall#(name,args) 
}
</xform>

<*********************>
<***** These code templates are used to build AST *>
<*********************>

<code MIN pars=(first,second)>
min(@first@,@second@)
</code>
<code MAX pars=(first,second)>
max(@first@,@second@)
</code>

<code ArrayAccess pars=(array:EXP,subscript:ExpList)>
@array@(@subscript@)
</code>

<code Loop pars=( i, start, stop, step)>
@i@=@start@, @stop@, @step@
</code>

<code NestLoop pars=(ctrl:CODE.While|CODE.ParseLoop, body:LIST(Stmt,"\n"))>
@CODE.DO@ @ctrl@
@body@
enddo
</code>

<code Nest pars=(ctrl, body:LIST(Stmt,"\n")) match=CODE.Stmt>
@(switch (ctrl) {
  case CODE.Loop|CODE.While: NestLoop#(ctrl, body)
  case CODE.If: NestIf#(ctrl, body,"endif")
  case CODE.UnrolledLoopCheck#(_,i1,i2) : NestIf#(Bop#("<",i1,i2),body,"endif")
})@

</code>

<*********************>
<***** These code templates are used for parsing Fortran code *>
<*********************>

<code SingleLoop parse=(CODE.NestDO|"") />

<code LoopNestList2 parse=LIST(CODE.NestDO|CODE.Label,"\n")/>
<code LoopNestList pars=(content:LoopNestList2)>
@content@

</code>

<code ParseLoop pars=( i:ID, start:EXP, stop:EXP, step:CODE.LoopStep|"")
           rebuild=(Loop#(i,start,stop, ((step=="")? 1 : step)))>
@i@=@start@, @stop@@step@
</code>

<code While pars=(condition:EXP) parse=(("while"|"WHILE") "(" condition ")") >
while (@condition@)
</code>

<code DO match="DO"|"do" parse="DO"|"do"> do </code> 

<code If pars=(condition:EXP) parse=(("if"|"IF") "(" condition ")" ("then"|"THEN"|""))>
if (@condition@) then
</code>

<code Type2 pars=(type : Name, len : INT) >
@type@*@len@
</code>

<code LoopStep pars=(val: EXP) rebuild=val > , @val@ </code>

<xform ParseLoopBody pars=(input) output=(_res,_left) label="">
stop = (label=="")? ("enddo"|"ENDDO") : label;
(res,left) = ParseList[code=CODE.Stmt;stop=stop](input);
stopToken = FirstToken(left);
if (!(stopToken : stop)) {
   ERROR("Unexpected stopping token:" stopToken "; expecting:" stop "\n parse result is : " res "\n leftover is: " left);
}
left2=NextToken(left);
if (stop == label && !(FirstToken(left2) : "CONTINUE"|"continue")) 
   { <<* QY: include stmt as part of loop body
      left2 => (next=CODE.Stmt,left2=_);
      res=BuildList(res,next);
      left = "\n"::stop::"continue"::"\n"::left2;
   }
(res,left)
</xform>

<code NestDO pars=(loop: CODE.While | CODE.ParseLoop, 
                      body, label : INT|"")
      rebuild=(CODE.Nest#(loop, body)) >
@CODE.DO@ @label@ @loop@
  @(body=XFORM.ParseLoopBody[label=label])@
@("enddo"|"ENDDO"|"")@
</code>

<code EndElse pars=(body : LIST(Stmt,"\n"))>

@body@
endif
</code>

<code WithElse pars=(rest : CODE.NestIf | EndElse) parse=( ("ELSE"|"else") rest) 
               output=("ELSE" rest)  />

<code NestIf pars=(ctrl:CODE.If, body:LIST(Stmt,"\n"), rest:"endif"|"ENDIF"|WithElse|"")>
@ctrl@
@body@
@rest@
</code>

<code Assign pars=(lhs:EXP, rhs:EXP) >
@lhs@=@rhs@
</code>

<code ExpStmt pars=(exp:EXP) > 
@exp@
</code>

<code FunctionCall pars=(function:ID, args : CODE.ExpList)>
@function@(@args@)
</code> 

<code CallStmt pars=(func : FunctionCall)>
@CODE.Call@ @func@
</code>

<code Call parse="call"|"CALL" output="call"/>

<code Return pars=(val : EXP | "") >
@CODE.ReturnK@ @val@
</code>

<code ReturnK parse="return"|"RETURN" output="return"/>

<code Label pars=(val:INT) parse=(val ("continue"|"CONTINUE"))> 
@val@ continue

</code>

<*********************************** Types ************************************>

<code Type parse=CODE.IntegerType | CODE.DoubleType | CODE.RealType | CODE.ComplexType| CODE.CharacterType | "LOGICAL" | CODE.ExternalType| CODE.ProcedureType/>

<code IntegerType pars=(val : "integer"|"INTEGER") > @val@ </code>
<code DoubleType pars=(val : ("double" "precision") | ("DOUBLE" "PRECISION"))> @val@ </code>
<code RealType pars=(name : "real"|"REAL", prec : CODE.Precision|"") >
@name prec@
</code>
<code ComplexType pars=(name : "complex"|"COMPLEX", prec : CODE.Precision|"") >
@name prec@
</code>
<code CharacterType pars=(len : CODE.LenSpec|"")> CHARACTER @len@ </code>
<code LenSpec pars=(len : EXP)> (LEN=@len@) </code>



<code IntType pars=(name:"int")>@ (name=="int")? IntegerType : name @</code>
<code FloatType pars=(name:"double")>@ (name=="double")? DoubleType : name @</code>

<******************************* Identifiers **********************************>

<code ArrayDim pars=(dim:"*"|LIST(CODE.Range,",")) rebuild=dim> (@dim@) </code>
<code Range pars=(begin :EXP|"", end : CODE.EndRange|"") 
           rebuild=((end == "")? begin : Range#(begin,end)) > @begin end@ </code>
<code EndRange pars=(val : EXP|"") > : @val@ </code>

<code Variable pars=(name:CODE.Name, init : CODE.ArrayDim | CODE.VarInit | "")>
@name@@init@
</code>

<code VariableList parse=LIST(Variable,",") />

<code VarInit pars=(val : EXP)> = @val@ </code>

<xform InsertParam pars=(var) tab=GLOBAL.SymTable>
if (tab == NULL) { GLOBAL.SymTable = tab = MAP{}; }
tab=car(tab);
tab[var]=TAG#("PARAM",tab[var]);
</xform>

<code VariableListStmt pars=(content:VariableList) rebuild=content >
@content@

</code>

<code WriteStmt pars=(format: LIST(CODE.Char|"*"|ID,","), what : LIST(EXP,","))>
WRITE (@format@) @what@
</code>

<code Format parse=LIST(INT|ID|FLOAT, "")/>


<******************************* Declarations *********************************>

<code ParamDecl pars=(var:ID, val:INT) 
      rebuild=(InsertParam(var);ParamDecl#(var,val))> @var@=@val@ </code>

<code ParamDeclStmt
      pars=(keyword:"PARAMETER"|"parameter", vars:LIST(ParamDecl,","))>
@keyword@ (@vars@)

</code>

<code ImplicitDeclStmt
      pars=(keyword:"IMPLICIT"|"implicit", type:Name, ids:EXP)>
@keyword@ @type@ (@ids@)

</code>

<code ExternalType pars=(name: ID)> TYPE(@name@) </code>
<code ProcedureType pars=(name: ID)> PROCEDURE(@name@) </code>

<code TypeDecl pars=(t : CODE.Type, annot : CODE.Annotations|"")> @t annot@ </code>
<code Annotations pars=(annot : LIST("POINTER"|"PRIVATE"|"OPTIONAL"|"ALLOCATABLE"|"TARGET"|CODE.DimensionDecl,","))> ,@annot@ </code>
<code DimensionDecl pars=(d:LIST(EXP,","))> DIMENSION(@d@) </code>
<code VarDeclStmt pars=(type:CODE.TypeDecl, var:VariableListStmt) 
       parse=(type ("::"|"") var) rebuild=(VarDeclStmt#(insert_typeInfo(type,var))) >
@type@ :: @var@
</code>

<code ParamName pars=(id:ID) rebuild=(insert_typeInfo("",ParamName#(id)))> 
@id@ </code>

<code Subroutine parse="subroutine"|"SUBROUTINE"|"FUNCTION"|"function" output="subroutine" />

<code StmtBlock pars=(stmts:LIST(CODE.Stmt,"\n")) symtab=""
  match=CODE.SingleStmt
  parse=(eval(XFORM.enter_block("NEW")) stmts 
         eval(return(StmtBlock[symtab=XFORM.exit_block(stmts)]#stmts)))
  > @stmts@ </code>

<code SubroutineDecl pars=(name:ID, params:LIST(ParamName,","), body:CODE.StmtBlock) match=(CODE.FunctionDecl#(name,params,"void",body)) 
   parse=(CODE.Subroutine name "(" params ")" body CODE.End (CODE.Subroutine|"") (name|"")) >
subroutine @name@(@params@)
  @body@
end subroutine @name@

</code>

<code End parse="end"|"END" output="end" />

<***************************** OMP Instructions *******************************>

<* \a private and \a shared are list of variable names *>
<code OMPParallelDo>
!$omp@@parallel do

</code>

<code OMPContinue pars=(content)>
!$omp^@content@

</code>

<code PrivateNameList pars=(vars:NameList)>
private(@vars@)
</code>

<code SharedNameList pars=(vars:NameList)>
shared(@vars@)
</code>

<* Variables are seperated into lines to avoid going beyond limited columns. *>
<code OMPParallelBegin pars=(private, shared)>
@ ret=OMPParallelDo;
for (rest=private; rest!=NULL; rest=cdr rest)
  ret=BuildList(OMPContinue#(PrivateNameList#(car rest)), ret);
for (rest=shared; rest!=NULL; rest=cdr rest)
  ret=BuildList(OMPContinue#(SharedNameList#(car rest)), ret);
ReverseList(ret) @
</code>

<code OMPParallelEnd>
!$omp end parallel do

</code>

<code OMPDecl pars=(id:ID, type:ID) >
 @type@ @id@

</code>


<code OMPSetNested pars=(num:EXP) >
!$ call omp_set_nested(@num@)

</code>

<code OMPSetNumThreads pars=(num:EXP)>
!$ call omp_set_num_threads(@num@)

</code>

<code OMPSetSchedule pars=(schedule:EXP, num:EXP) >
!$ call omp_set_schedule(@schedule@, @num@)

</code>


<***************************** PAPI Instructions ******************************>
<code PAPIIncl>

include "f90papi.h"

</code>

<code PAPIDecl>

INTEGER counters(4), ncounters, chkflg
INTEGER*8 values(4)
DOUBLE PRECISION t1, t2, OMP_GET_WTIME
EXTERNAL OMP_GET_THREAD_NUM

</code>

<code PAPIInit pars=(counters:NameList)>

chkflg = PAPI_VER_CURRENT 
call PAPIF_library_init(chkflg)
if (chkflg .ne. PAPI_VER_CURRENT) then
Write(0, *) 'Error PAPI Library init fail', PAPI_VER_CURRENT
call abort
end if
call PAPIF_thread_init(omp_get_thread_num, error)
if(error .ne. PAPI_OK) then
Write(0, *) 'PAPI with OpenMP fails'
call abort
end if
counters(1) = @car(counters)@;
counters(2) = @car(cdr(counters))@;
counters(3) = @car(cdr(cdr(counters)))@;
counters(4) = @car(cdr(cdr(cdr(counters))))@;

ncounters = @LEN(counters)@;
call PAPIF_START_COUNTERS(counters, ncounters, chkflg)
t1 = OMP_GET_WTIME()

</code>

<code PAPIFinalize pars=(counters:NameList)>

t2 = OMP_GET_WTIME()
call PAPIF_STOP_COUNTERS(values, ncounters, chkflg);
WRITE(0,*) "TIME : ", t2-t1, "seconds"
WRITE(0,*) "@car(counters)@ : ", values(1)
WRITE(0,*) "@car(cdr(counters))@ : ", values(2)
WRITE(0,*) "@car(cdr(cdr(counters)))@ : ", values(3)
WRITE(0,*) "@car(cdr(cdr(cdr(counters))))@ : ", values(4)

</code>

<**************************** Fortran Instructions ****************************>

<define INCLUDE CODE.Include />

<code SystemHeaders pars=(file:STRING)>
<@file@>
</code>

<code UserHeaders pars=(file:STRING)>
"@file@"
</code>

<code Headers pars=(content:SystemHeaders|UserHeaders|STRING)>
@content@
</code>

<code Include pars=(content:Headers)>
include @content@
</code>

<****************************** OpenMP Adaptors *******************************>

<code OMP_MACRO pars=(content:OMP_INST) rebuild=(XFORM.RebuildOMPMacro(content))>
!$omp @content@
</code>

<code OMPMacroCont pars=(content:UnknownUntilEOL)>
!$omp^ @content@

</code>

<xform RebuildOMPMacro pars=(input)>
<<* jichi (4/30/2010): Rebuild macro to prevent Fortran from columns overflow.
switch(input) {
case OMPParallelLoop#(content):
  BuildList(OMPMacro#(OMPParallelLoop#(Nothing)),
            RebuildOMPMacro(content))
case OMPParallelSection#(content):
  BuildList(OMPMacro#(OMPParallelSection#(Nothing)),
            RebuildOMPMacro(content))
case OMPAttrList#(attr):
  ret = NULL;
  foreach (attr : (shared = OMPSharedNameList) : TRUE)
    ret = RebuildOMPMacro(shared) :: ret;
  foreach (attr : (private = OMPPrivateNameList) : TRUE)
    ret = RebuildOMPMacro(private) :: ret;
  foreach (attr : (reduction = OMPReductionNameList) : TRUE)
    ret = RebuildOMPMacro(reduction) :: ret;
  foreach (attr : (schedule = OMPSchedule) : TRUE)
    ret = OMPMacroCont#(schedule) :: ret;
  foreach (attr : (threads = OMPNumThreads) : TRUE)
    ret = OMPMacroCont#(threads):: ret;
  ret

case OMPSharedNameList#(vars):
  (!(vars:NameList#(l)))? OMPMacroCont#(OMPSharedNameList#(vars))
  : (
    ret = NULL;
    for (rest = l; rest!=NULL; rest = cdr rest)
      ret = OMPMacroCont#(OMPSharedNameList#(car rest)) :: ret;
    ret
  )

case OMPPrivateNameList#(vars):
  (!(vars:NameList#(l)))? OMPMacroCont#(OMPPrivateNameList#(vars))
  : (
    ret = NULL;
    for (rest = l; rest!=NULL; rest = cdr rest)
      ret = OMPMacroCont#(OMPPrivateNameList#(car rest)):: ret;
    ret
  )

case OMPReductionNameList#(op, vars):
  (!(vars:NameList#(l)))? OMPMacroCont#(OMPReductionNameList#(op, vars))
  : (
    ret = NULL;
    for (rest = l; rest!=NULL; rest = cdr rest)
      ret = OMPMacroCont#(OMPReductionNameList#(op, car rest)) :: ret;
    ret
  )

default: PRINT(input); ERROR("unknown input type");
}
</xform>
